---
page_title: "Data Source: auth0_action_module_actions"
description: |-
  Data source to retrieve all actions that are using a specific Auth0 action module.
---

# Data Source: auth0_action_module_actions

Data source to retrieve all actions that are using a specific Auth0 action module.

## Example Usage

```terraform
# Example: Retrieve all actions using a specific action module

# Create and publish an action module
resource "auth0_action_module" "my_module" {
  name    = "My Shared Module"
  publish = true
  code    = <<-EOT
    module.exports = {
      greet: function(name) {
        return "Hello, " + name + "!";
      }
    };
  EOT
}

# Get the published versions of the module
data "auth0_action_module_versions" "my_module_versions" {
  module_id = auth0_action_module.my_module.id
}

# Create an action that uses the module
resource "auth0_action" "my_action_1" {
  name   = "My Action Using Module 1"
  deploy = true
  code   = <<-EOT
    const myModule = require('my-module');
    exports.onExecutePostLogin = async (event, api) => {
      console.log(myModule.greet(event.user.name));
    };
  EOT

  supported_triggers {
    id      = "post-login"
    version = "v3"
  }

  modules {
    module_id         = auth0_action_module.my_module.id
    module_version_id = data.auth0_action_module_versions.my_module_versions.versions[0].id
  }
}

# Create another action that uses the same module
resource "auth0_action" "my_action_2" {
  name   = "My Action Using Module 2"
  deploy = true
  code   = <<-EOT
    const myModule = require('my-module');
    exports.onExecutePostLogin = async (event, api) => {
      api.idToken.setCustomClaim("greeting", myModule.greet(event.user.name));
    };
  EOT

  supported_triggers {
    id      = "post-login"
    version = "v3"
  }

  modules {
    module_id         = auth0_action_module.my_module.id
    module_version_id = data.auth0_action_module_versions.my_module_versions.versions[0].id
  }
}

# Retrieve all actions that are using this module
data "auth0_action_module_actions" "my_module_actions" {
  depends_on = [auth0_action.my_action_1, auth0_action.my_action_2]
  module_id  = auth0_action_module.my_module.id
}

# Output the total number of actions using this module
output "actions_using_module" {
  value = data.auth0_action_module_actions.my_module_actions.total
}

# Output the list of actions
output "action_names" {
  value = [for action in data.auth0_action_module_actions.my_module_actions.actions : action.action_name]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `module_id` (String) The ID of the action module.

### Read-Only

- `actions` (List of Object) List of actions using this module. (see [below for nested schema](#nestedatt--actions))
- `id` (String) The ID of this resource.
- `total` (Number) The total number of actions using this module.

<a id="nestedatt--actions"></a>
### Nested Schema for `actions`

Read-Only:

- `action_id` (String)
- `action_name` (String)
- `module_version_id` (String)
- `module_version_number` (Number)
- `supported_triggers` (List of Object) (see [below for nested schema](#nestedobjatt--actions--supported_triggers))

<a id="nestedobjatt--actions--supported_triggers"></a>
### Nested Schema for `actions.supported_triggers`

Read-Only:

- `id` (String)
- `version` (String)


